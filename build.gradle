buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'foundation.icon:gradle-javaee-plugin:0.7.8'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

allprojects {
    apply plugin: 'jacoco'
    jacoco {
        toolVersion = "0.8.7"
    }

    repositories {
        jcenter()
        mavenLocal()
    }
}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'foundation.icon.javaee'
    apply plugin: 'jacoco'

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    // need to add this option to retrieve formal parameter names
    compileJava {
        options.compilerArgs += ['-parameters']
    }

    def jacocoExecutionDataFiles = fileTree(buildDir).include("/jacoco/*.exec")

    jacocoTestReport {
        executionData.setFrom jacocoExecutionDataFiles
    }
}

task generateMergedReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['score/**',
                              'exchange/convexus/utils/**',
                              'com/iconloop/score/test/**',
                              'exchange/convexus/testtokens/**',
                              'exchange/convexus/testgauges/**',
                              'com/iconloop/score/util/**',
                              'com/iconloop/score/token/irc31/**',
                              'com/iconloop/score/token/irc3/**',
                              'com/iconloop/score/token/irc2/**',
                    ])
        })
    }
}